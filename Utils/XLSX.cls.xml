<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="Utils.XLSX">
<IncludeCode>%occODBC</IncludeCode>
<TimeCreated>64194,38459.023377</TimeCreated>

<Method name="generateFileFromSQL">
<Description>
Outputs Excel-compliant table from sql into file. args are sql arguments
Does not support
w ##class(Utils.XLSX).generateFileFromSQL(,"SELECT * FROM Sample.Person")</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>file:%String=##class(%File).TempFilename("xlsx"),sql:%String,args...</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim sc As %Status = $$$OK
	
	set xmlfile = ##class(%File).TempFilename("xml")
	set sc = ..generateXMLFromSQL(xmlfile, sql, args...)
	quit:$$$ISERR(sc) sc
	
	set sc = ..generateXLSXfromXML(xmlfile, file)
	quit sc
]]></Implementation>
</Method>

<Method name="generateXMLFromSQL">
<Description>
Outputs XML to be fed into %SYS.ZENReportExcelExporter Excel-compliant stream from sql into file. args are sql arguments</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>xmlfile:%String,sql:%String,args...</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim sc As %Status = $$$OK
	set st = ##class(%SQL.Statement).%New()
	//set st.%SelectMode = 1 // ODBC
	set sc = st.%Prepare(sql)
	quit:$$$ISERR(sc) sc
	
	#dim result As %SQL.StatementResult
	set result = st.%Execute(args...)
	
	set stream = ##class(%Stream.FileCharacter).%New()
	set sc=stream.LinkToFile(xmlfile)
	
	set header = ##class(%Dictionary.XDataDefinition).IDKEYOpen($classname(), "header").Data.Read(10000)
	do stream.Write(header)

	#dim metadata As SQL.StatementMetadata
	set metadata = result.%GetMetadata()
	set columnCount = metadata.columns.Count()
	for i=1:1:columnCount {
		#dim column As %SQL.StatementColumn
		set column = metadata.columns.GetAt(i)
		
		set class(i) = "<item excelName="""_ column.colName _ """"
		if column.IsNumeric() {
			set class(i) = class(i) _ " isExcelNumber=""1"""
		} elseif ..isDate(column.ODBCType) {
			set class(i) = class(i) _ " isExcelDate=""1"""
		} elseif ..isTime(column.ODBCType)  {
			set class(i) = class(i) _ " isExcelTime=""1"""	
		}
		set class(i) = class(i) _ ">"
	}
	
	while result.%Next() {
		do stream.WriteLine("<Row>")
		for i=1:1:columnCount {
			set data = result.%GetData(i)
			set:$lv(data) data = $lts(data)
			do stream.WriteLine(class(i) _ data _ "</item>")
		}
		do stream.WriteLine("</Row>")
	}
	
	do stream.WriteLine("</sheet></MyReport>")
	do stream.%Save()
	return sc
]]></Implementation>
</Method>

<Method name="generateXLSXfromXML">
<ClassMethod>1</ClassMethod>
<FormalSpec>xmlfile:%String,file:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set exportType = "xlsx"
	set outputdir = ##class(%File).GetDirectory(file)
	set outputfile = file
	set isMultiSheet = $$$YES
	set aggregateTag = ""
	set reportName = "Report"
	set logfile = ##class(%File).TempFilename("log")
	set suppresExcelHeaders = $$$NO
	set ExcelMode = "element"
	set DisplayLog= $$$NO
	
	set sc = ##class(%SYS.ZENReportExcelExporter).CommonExport(exportType, xmlfile, outputdir, outputfile, isMultiSheet, aggregateTag, reportName, logfile, suppresExcelHeaders, ExcelMode, DisplayLog)
	return sc
]]></Implementation>
</Method>

<Method name="isDate">
<ClassMethod>1</ClassMethod>
<FormalSpec>odbcType:%Integer</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	return:odbcType=$$$ODBCTYPEtimestamp $$$YES
	return:odbcType=$$$ODBCTYPEdate $$$YES
	return $$$NO
]]></Implementation>
</Method>

<Method name="isTime">
<ClassMethod>1</ClassMethod>
<FormalSpec>odbcType:%Integer</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	return:odbcType=$$$ODBCTYPEtime $$$YES
	return $$$NO
]]></Implementation>
</Method>

<XData name="header">
<Data><![CDATA[
<?xml version="1.0" encoding="UTF-8"?>
<MyReport xmlns:fo="http://www.w3.org/1999/XSL/Format" xmlns:xlink="http://www.w3.org/1999/xlink" xmlns:zr="http://www.intersystems.com/zenreports" xmlns:xs="http://www.w3.org/2001/XMLSchema" xmlns:svg="http://www.w3.org/2000/svg">
<sheet excelSheetName="Persons">
]]></Data>
</XData>
</Class>
</Export>
